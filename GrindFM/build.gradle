buildscript {
    ext {
        verKotlin = '1.0.3-eap-68'
        verSupport = '24.0.0'
        verPlayServices = '9.2.0'
        verRetrofit = '2.1.0'
    }
    repositories {
        jcenter()
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.0-alpha4'
        classpath 'com.google.gms:google-services:3.0.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.12.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$verKotlin"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.ben-manes.versions'

android {
    compileSdkVersion 24
    buildToolsVersion '23.0.3'

    defaultConfig {
        applicationId "fm.grind.android"

        minSdkVersion 16
        targetSdkVersion 24
        versionCode 11
        versionName "0.5"
    }

    signingConfigs {
        release {
            storeFile file('/Users/dsvoronin/Dropbox/private/dsvoronin.keystore')
            keyAlias "key-grind"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            proguardFile 'proguard.cfg'
        }
    }

    if (project.hasProperty('storeFile') && project.hasProperty('storePassword') && project.hasProperty('keyPassword')) {
        android.signingConfigs.release.storeFile = file(storeFile)
        android.signingConfigs.release.storePassword = storePassword
        android.signingConfigs.release.keyPassword = keyPassword
    } else {
        android.buildTypes.release.signingConfig = null
    }
}

repositories {
    jcenter()
}

dependencies {
    compile "com.android.support:appcompat-v7:$verSupport"
    compile "com.android.support:support-v4:$verSupport"
    compile "com.android.support:design:$verSupport"
    compile "com.android.support:support-annotations:$verSupport"
    compile "com.android.support:cardview-v7:$verSupport"
    compile "com.android.support:recyclerview-v7:$verSupport"

    compile "com.google.android.gms:play-services-analytics:$verPlayServices"
    compile "com.google.android.gms:play-services-gcm:$verPlayServices"

    compile 'com.squareup.okhttp3:okhttp:3.3.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.2'
    compile "com.squareup.retrofit2:retrofit:$verRetrofit"
    compile "com.squareup.retrofit2:converter-gson:$verRetrofit"
    compile("com.squareup.retrofit2:converter-simplexml:$verRetrofit") {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }

    compile 'com.facebook.stetho:stetho:1.3.1'
    compile 'com.facebook.stetho:stetho-okhttp3:1.3.1'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'

    testCompile 'junit:junit:4.12'
    testCompile "org.robolectric:robolectric:3.1"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2"
}

apply plugin: 'com.google.gms.google-services'
